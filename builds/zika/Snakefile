rule parse:
	input:
		"../../../fauna/data/zika.fasta"
	output:
		seq = "results/raw_seqs.fasta",
		meta = "results/meta.tsv"
	params:
		fields = "strain virus accession date region country division city db segment authors paper_url"
	shell:
		'augur parse --sequences {input} --output {output.seq} --metadata {output.meta} --fields {params.fields}'


rule filter:
	input:
		seq = "results/raw_seqs.fasta",
		meta = "results/meta.tsv",
		exclude = "metadata/dropped_strains.txt"
	output:
		"results/filtered.fasta"
	params:
		vpc = 30,
		cat = "year month"
	shell:
		"augur filter --sequences {input.seq} --output {output} --metadata {input.meta} --viruses_per_cat {params.vpc} --exclude {input.exclude} --cat {params.cat}"


rule align:
	input:
		seq = "results/filtered.fasta",
		ref = "metadata/zika_outgroup.gb"
	output:
		"results/aligned.fasta"
	shell:
		'augur align --sequences {input.seq} --output {output} --reference_sequence {input.ref}'


rule tree:
	input:
		aln = "results/aligned.fasta",
	output:
		tree = "results/tree_raw.nwk",
	shell:
		'augur tree --alignment {input.aln} --output {output.tree}'


rule timetree:
	input:
		aln = "results/aligned.fasta",
		metadata = "results/meta.tsv",
		tree = rules.tree.output.tree,
	output:
		node_data = "results/node_data.json",
		tree = "results/tree.nwk",
	shell:
		'augur treetime --tree {input.tree} --alignment {input.aln} --metadata {input.metadata}'
			' --output {output.tree} --node_data {output.node_data}'
			' --timetree --date_confidence'

rule traits:
	input:
		tree = "results/tree.nwk",
		metadata = "results/meta.tsv"
	output:
		"results/traits.json",
	params:
		cols = "region country"
	shell:
		'augur traits --confidence --tree {input.tree} --metadata {input.metadata} --output {output} --columns {params.cols}'

rule translate:
	input:
		tree = "results/tree.nwk",
		ref = "metadata/zika_outgroup.gb",
		node_data = "results/node_data.json"
	output:
		"results/aa_muts.json"
	shell:
		'augur translate --tree {input.tree} --node_data {input.node_data} --output {output} --reference_sequence {input.ref}'

rule export:
	input:
		tree = "results/tree.nwk",
		node_data = "results/node_data.json",
		metadata = "results/meta.tsv",
		traits = "results/traits.json",
		aa_muts = "results/aa_muts.json",
		color_defs = "metadata/colors.tsv",
		config = "metadata/config.json",
		geo_info = "../../../fauna/source-data/geo_lat_long.tsv"
	output:
		tree = "auspice/zika_tree.json",
		meta = "auspice/zika_meta.json",
	shell:
		'augur export --tree {input.tree} --metadata {input.metadata}'
			' --node_data {input.node_data} --traits {input.traits} --aa_muts {input.aa_muts}'
			' --config {input.config} --color_defs {input.color_defs} --output {output.tree}'
			' --geo_info {input.geo_info} --meta_output {output.meta}'

